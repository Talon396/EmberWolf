(include "Sources.hvos")
(externFn halt)

(fn main ()
    (TestFB)
    (SetupTrap (TrapHandler) 0)
    (FramebufferRenderBackground)
    (FramebufferDrawCenteredRect 510 382 384 512 255)
    (FramebufferDrawCenteredRect 514 386 384 512 248)
    (FramebufferDrawCenteredRect 512 384 384 512 252)

    (FramebufferDrawCenteredRect 512 384 336 464 250)
    (FramebufferDrawCenteredRect 345 384 2 464 249)
    (FramebufferDrawCenteredRect 512 152 336 2 249)
    (FramebufferDrawCenteredRect 679 383 2 464 255)
    (FramebufferDrawCenteredRect 512 615 336 2 255)
    (FramebufferRenderMonochromeBitmap 384 150 128 128 2 0 0 (OkamiLogo))
    (FramebufferRenderString 416 408 2 0 "OkamiStation")
    (UpdateSelfTestStatus "Testing RAM" (RAMIcon))
    (TestRAM)
    (UpdateSelfTestStatus "Initializing Hardware Trap Controller" 0)
    (UpdateSelfTestStatus "Diagnostics were successful!" 0)

    (BootMenuInit)

    (halt)
)

(fn Exception (context cause pc vaddr)
    (int i strSize)
    (if (u> cause 1) (do
        (ConsoleInit)
        (ConsolePrint "EXCEPTION TRAP TRIGGERED - Cause 0x")
        (itoa cause (stringBuffer) 16)
        (ConsolePrint (stringBuffer))
        (ConsolePrint "\n===== REGISTER DUMP =====\n")
        (= i 0)
        (while (u< i 19)
            (memset16 (stringBuffer) 96 0)
            (itoa (w@ (+ context (u* i 4))) (stringBuffer) 16)
            (= strSize (- 8 (strlen (stringBuffer))))
            (ConsolePrint "0x")
            (while (u> strSize 0) (ConsolePrint "0") (= strSize (- strSize 1)))
            (ConsolePrint (stringBuffer))
            (ConsolePrint " ")
            (if (== (u% i 4) 3) (do
                (ConsolePrint "\n")
            ))
            (= i (+ i 1))
        )
        (ConsolePrint "\nPC: 0x")
        (itoa pc (stringBuffer) 16)
        (ConsolePrint (stringBuffer))
        (ConsolePrint "\n\nSystem Halted\n")
        (halt)
    ))
)

(var FWstack 4096)
(var FWbss_end 0)