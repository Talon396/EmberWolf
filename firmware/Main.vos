(include "Sources.hvos")
(externFn halt)

(fn main ()
    (int code 0)
    (TestRAM)
    (TestFB)
    (SetupTrap (TrapHandler) 0)
    (SetupTrap (TrapHandler) 1)

    (FramebufferRenderBackground)
    /* Run POST */
    (FramebufferDrawCenteredRect 510 382 320 128 255)
    (FramebufferDrawCenteredRect 514 386 320 128 248)
    (FramebufferDrawCenteredRect 512 384 320 128 252)
    (FramebufferRenderMonochromeBitmap 352 320 128 128 1 0 0 (OkamiLogo))
    (FramebufferRenderMonochromeBitmap 560 320 32 32 4 0 1 (OkamiStation))
    (FramebufferRenderMonochromeBitmap 490 369 64 31 1 0 0 (SystestText))
    (= code (SCSISelect 0))
    (if (== code 1) (SelfTestProblem "Arbitration In Progress Bit stuck low on NCR8350"))
    (ConsoleInit)
    (FramebufferRenderMonochromeBitmap 192 144 128 128 1 255 0 (OkamiLogo))
    (ConsolePrint "\n\n\n                 OkamiStation 1000 (Okami1041, 25 MHz), Keyboard Present\n                 OkamiBoot 1.0, ")
    (itoa (u/ RAMSize 1048576) (stringBuffer) 10)
    (ConsolePrint (stringBuffer))
    (ConsolePrint " MB memory installed\n\n\n\n")
    (ConsolePrint "NVRAM is corrupted!\n")
    (FORTH)

    (halt)
)

(fn Exception (context cause pc vaddr)
    (int i strSize)
    (FramebufferDither 0)
    (ConsoleInit)
    (ConsolePrint "EXCEPTION TRAP TRIGGERED - Cause 0x")
    (itoa cause (stringBuffer) 16)
    (ConsolePrint (stringBuffer))
    (ConsolePrint "\n===== REGISTER DUMP =====\n")
    (= i 0)
    (while (u< i 19)
        (memset16 (stringBuffer) 96 0)
        (itoa (w@ (+ context (u* i 4))) (stringBuffer) 16)
        (= strSize (- 8 (strlen (stringBuffer))))
        (ConsolePrint "0x")
        (while (u> strSize 0) (ConsolePrint "0") (= strSize (- strSize 1)))
        (ConsolePrint (stringBuffer))
        (ConsolePrint " ")
        (if (== (u% i 4) 3) (do
            (ConsolePrint "\n")
        ))
        (= i (+ i 1))
    )
    (ConsolePrint "\nPC: 0x")
    (itoa pc (stringBuffer) 16)
    (ConsolePrint (stringBuffer))
    (ConsolePrint "\n\nSystem Halted\n")
    (halt)
)

(var FWstack 4096)
(var FWbss_end 0)