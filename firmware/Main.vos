(include "Sources.hvos")
(externFn halt)

(fn main ()
    (int nvramCorrupted a i)
    (w! 0xbe000004 (! (strncmp "\x80OkBoot\x80" 0xbe001000 8)))
    (if (| (w@ 0xbe000004) (b@ 0xbe001011)) (TestRAM) (= RAMSize (w@ 0xbe001018)))
    (= nvramCorrupted (w@ 0xbe000004))
    (w! 0xbe000004 0)
    (TestFB)
    (SetupTrap (TrapHandler) 0)
    (SetupTrap (TrapHandler) 1)

    (FramebufferRenderBackground)
    (FramebufferRenderString 0 752 1 255 "Rev 0.1")
    (if (| nvramCorrupted (b@ 0xbe001010)) (do
        /* Run POST */
        (FramebufferDrawCenteredRect 510 382 320 128 255)
        (FramebufferDrawCenteredRect 514 386 320 128 248)
        (FramebufferDrawCenteredRect 512 384 320 128 252)
        (FramebufferRenderMonochromeBitmap 352 320 128 128 1 0 0 (OkamiLogo))
        (FramebufferRenderMonochromeBitmap 560 320 32 32 4 0 1 (OkamiStation))
        (FramebufferRenderMonochromeBitmap 490 369 64 31 1 0 0 (SystestText))
        (SetTimeout 5000 "NCR5380 Arbitration & Select Timeout (check SCSI Controller!)")
        (if (u> (SCSISelect 0 7 0) 0) (SelfTestProblem "NCR5380 Busy line stuck low during selection (check SCSI Controller!)"))
        (= i 0)
        (while (u< i 1048576) (w! 0xbe000000 0xffffffff) (= i (+ i 1)))
    ))
    (= a 256)
    (= i 0)
    (while (!= a 0) 
        (= a (w@ 0xbe000040))
        (if a (= i a))
    )
    (if (== i 0x3A) (BootROM nvramCorrupted 2))
    (ConsoleInit)
    (memset (wordBuf) 9 0)
    (memcpy (wordBuf) 0xbe001008 8)
    (Boot nvramCorrupted)
    (halt)
)

(fn Boot (nvramCorrupted)
    (if (| nvramCorrupted (strncmp (wordBuf) "rom" 3)) (BootROM nvramCorrupted 0)
        (strncmp (wordBuf) "scsi" 4) (do
            (ConsolePrint "Booting from SCSI Drive #")
            (ConsolePrint (+ (wordBuf) 4))
            (ConsolePrint "...\n\n")
            (SCSISelect 0 7 0)
        )
        (BootROM nvramCorrupted 1)
    )
)

(fn BootROM (nvramCorrupted badOption)
    (ConsoleInit)
    (FramebufferRenderMonochromeBitmap 192 144 128 128 1 255 0 (OkamiLogo))
    (ConsolePrint "\n\n\n                 OkamiStation 1000 (Okami1041, 25 MHz), Keyboard Present\n                 OkamiBoot 1.0, ")
    (itoa (u/ RAMSize 1048576) (stringBuffer) 10)
    (ConsolePrint (stringBuffer))
    (ConsolePrint " MB memory installed\n\n\n\n")
    (if (== badOption 2) (ConsolePrint "Standard boot cycle was aborted!\n"))
    (if nvramCorrupted (ConsolePrint "NVRAM is corrupted!\n")
        (== badOption 1) (do (ConsolePrint "Invalid Boot Device: ") (ConsolePrint (wordBuf)) (ConsolePrint "\n"))
        )
    (FORTH)
)

(fn Exception (context cause pc vaddr)
    (int i strSize)
    (w! 0xbe000000 0xffffffff)
    (if (== cause 1) (do
        (SelfTestProblem timeoutMsg)
        (return)
    ))
    (FramebufferDither 0)
    (ConsoleInit)
    (ConsolePrint "EXCEPTION TRAP TRIGGERED - Cause 0x")
    (itoa cause (stringBuffer) 16)
    (ConsolePrint (stringBuffer))
    (ConsolePrint "\n===== REGISTER DUMP =====\na0-a3: ")
    (= i 0)
    (while (u< i 19)
        (memset16 (stringBuffer) 96 0)
        (itoa (w@ (+ context (u* i 4))) (stringBuffer) 16)
        (= strSize (- 8 (strlen (stringBuffer))))
        (ConsolePrint "0x")
        (while (u> strSize 0) (ConsolePrint "0") (= strSize (- strSize 1)))
        (ConsolePrint (stringBuffer))
        (ConsolePrint " ")
        (if (| (== (u% i 4) 3) (== i 17)) (do
            (ConsolePrint "\n")
            (if (== (u/ i 4) 0) (ConsolePrint "a4-a7: ") (== (u/ i 4) 1) (ConsolePrint "s0-s3: ") (== (u/ i 4) 2) (ConsolePrint "s4-s7: ") (== (u/ i 4) 3) (ConsolePrint "s8-s9: ") (== i 17) (ConsolePrint "   ra: "))
        ))
        (= i (+ i 1))
    )
    (ConsolePrint "\nPC: 0x")
    (itoa pc (stringBuffer) 16)
    (ConsolePrint (stringBuffer))
    (ConsolePrint "\n\nSystem Halted\n")
    (halt)
)

(var FWstack 4096)
(var FWbss_end 0)